apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jakewharton.hugo'

group = properties['project.group']
version = properties['project.version']
project.archivesBaseName = properties['project.artifactId']
def isRelease = !((String) project.properties.get('project.version')).contains("SNAPSHOT")
def isSnapshot = !isRelease

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

android {
    compileSdkVersion Integer.parseInt("" + project.properties.get('android.build_sdk_version'))
    buildToolsVersion project.properties.get('android.build_tools_version')

    defaultConfig {
        minSdkVersion project.properties.get('android.build_min_sdk_version')
        targetSdkVersion project.properties.get('android.build_target_sdk_version')
    }
    buildTypes {
        release {
            debuggable isSnapshot
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

bintray {
    user = project.properties.get('bintray.user')
    key = project.properties.get('bintray.apikey')

    configurations = ['archives']

    publish = true
    dryRun = isSnapshot

    pkg {
        repo = project.properties.get('bintray.repo')
        name = project.properties.get('bintray.package')
        userOrg = project.properties.get('bintray.organization')
        websiteUrl = project.properties.get('project.website')
        vcsUrl = project.properties.get('project.github')
        issueTrackerUrl = project.properties.get('project.issues')
        licenses = ["Apache-2.0"]
        version {
            mavenCentralSync {
                sync = true
                user = project.properties.get('nexus.user')
                password = project.properties.get('nexus.password')
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name project.properties.get('project.name')
                url project.properties.get('project.website')
                description project.properties.get('project.description')

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id project.properties.get('developer.id')
                        name project.properties.get('developer.name')
                        email project.properties.get('developer.email')
                    }
                }
                scm {
                    connection project.properties.get('project.github')
                    developerConnection project.properties.get('project.github')
                    url project.properties.get('project.website')
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

configurations {
    archives
}

artifacts {
    archives javadocJar
    archives sourcesJar
}